PREFIX politi: <http://politiRDF.com/>

PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX bd: <http://www.bigdata.com/rdf#>

#------------------------------Requete INSERT---------------------------------
# Requetes INSERT pour enrichir le graphe à partir de wikidata ou autres.

# Positions géographiques des départements.

INSERT {
    ?dep owl:sameAs ?wikidataDepartment ;
    	politi:coord ?coord.
} WHERE {
    service <https://query.wikidata.org/sparql> {
    	?wikidataDepartment wdt:P31 wd:Q6465;
        					wdt:P625 ?coord;
        					rdfs:label ?depLabel .
    }
    
	?dep a politi:Departement ;
		rdfs:label ?depLabel .
}

# Ajouter un lien owl:sameAs entre les deputes et leur entité Wikidata.

# 1. Récupérer les députés Wikidata en exercice depuis 2020 dans un graphe local
INSERT {
    GRAPH <http://cache/wikidata> {
        ?depute a politi:WikidataDepute ;
                rdfs:label ?deputeLabel ;
                schema:image ?img .
    }
}
WHERE {
    SERVICE <https://query.wikidata.org/sparql> {
        ?depute wdt:P39 wd:Q3044918 ;
                p:P39 ?statement ;
                rdfs:label ?deputeLabel ;
                wdt:P18 ?img .
        
        ?statement ps:P39 wd:Q3044918 ;
                   pq:P580 ?startTime .
        
        FILTER NOT EXISTS { ?statement pq:P582 ?endTime }
        FILTER(?startTime >= "2020-01-01"^^xsd:dateTime)
        FILTER(LANG(?deputeLabel) = "fr")
    }
}

# 2. Ajouter les liens owl:sameAs
INSERT {
    ?deputeLocal a politi:Depute ;
        schema:name ?depLocalLabel ;
        owl:sameAs ?depute .
}
WHERE {
    ?deputeLocal schema:name ?depLocalLabel .
    
    # Query local cache instead of federated query
    GRAPH <http://cache/wikidata> {
        ?depute a politi:WikidataDepute ;
                rdfs:label ?deputeLabel .
    }
    
    BIND(UCASE(?depLocalLabel) AS ?localNorm)
    BIND(UCASE(?deputeLabel) AS ?deputeNorm)
    FILTER(CONTAINS(?deputeNorm, ?localNorm) || CONTAINS(?localNorm, ?deputeNorm))
}


# Ajouter les partis
INSERT {
    ?dep schema:PoliticalParty ?wikiParti.

} WHERE {
    ?dep a politi:Depute.
    
    SERVICE <https://query.wikidata.org/sparql> {
    	?dep wdt:P102 ?wikiParti.
    }
}

#------------------------------Requete SELECT---------------------------------
#Requetes SELECT pour afficher et mettre en lien les données.

# Images des députés depuis Wikidata après ajout des liens owl:sameAs.

SELECT DISTINCT ?depLabel ?img
WHERE {
    ?dep a politi:Depute ;
        schema:name ?depLabel ;
        schema:image ?img .
}

# Nombre de députés par groupe parlementaire
# Utilisé à des fins de vérification après conversion des données.

SELECT  ?groupLabel (COUNT(?depu) AS ?nbDeputes)
WHERE {
    ?depu schema:memberOf ?group .
    ?group rdfs:label ?groupLabel .
} 
GROUP BY ?groupAbbrev ?groupLabel ?color
ORDER BY DESC(?nbDeputes)

# Toutes les statistiques des circonscriptions de l'Ain
# avec description de la statistique et source

SELECT ?cLabel ?statLabel ?val ?statSource
WHERE {
    ?c a politi:Circonscription ;
    	rdfs:label ?cLabel ;
    	schema:containedIn ?dep ;
    	?stat ?val .
    
    ?stat a politi:Statistic ;
    	rdfs:describe ?statLabel ;
    	rdfs:seeAlso ?statSource .
    
    ?dep rdfs:label ?depLabel .
    
    FILTER(?depLabel = "Ain"@fr)
} 
ORDER BY ?cLabel

# Toutes les statistiques des circonscriptions par député
# avec description de la statistique et source

SELECT ?depuLabel ?desc ?val ?source
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:name ?depuLabel .
    
    ?circo ?stat ?val .
    
    ?stat a politi:Statistic ;
    	rdfs:seeAlso ?source ;
    	rdfs:describe ?desc .
}
ORDER BY ?depuLabel

# Moyennes statistiques dans les circonscriptions, moyennées par groupe parlementaire

SELECT ?groupName (AVG(?val1) AS ?txPauvrete) (AVG(?val2) AS ?ageMoy) (AVG(?val3) AS ?accEcole) (AVG(?val4) AS ?inactRet) (AVG(?val5) AS ?inactEtu) (AVG(?val6) AS ?actempFem) (AVG(?val7) AS ?iltEtr) (AVG(?val8) AS ?popUrb)
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:memberOf ?group .
    
    ?group a politi:GroupeParlementaire ;
    	rdfs:label ?groupName.
    
    ?circo politi:txPauvrete60DiffTrageRF1 ?val1 ;
    	    politi:ageMoyen ?val2 ;
    	    politi:accEcole ?val3 ;
    	    politi:inactret ?val4 ;
    	    politi:inactetu ?val5 ;
    		politi:actempFem ?val6 ;
    		politi:iltEtr ?val7 ;
    		politi:popUrb ?val8 .
}
GROUP BY ?groupName
ORDER BY ?groupName


# Et moyennées par parti politique

SELECT ?partiLabel (AVG(?val1) AS ?txPauvrete) (AVG(?val2) AS ?ageMoy) (AVG(?val3) AS ?accEcole) (AVG(?val4) AS ?inactRet) (AVG(?val5) AS ?inactEtu) (AVG(?val6) AS ?actempFem) (AVG(?val7) AS ?iltEtr) (AVG(?val8) AS ?popUrb)
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:PoliticalParty ?parti .
    
    ?parti rdfs:label ?partiLabel .
    
    ?circo politi:txPauvrete60DiffTrageRF1 ?val1 ;
    	    politi:ageMoyen ?val2 ;
    	    politi:accEcole ?val3 ;
    	    politi:inactret ?val4 ;
    	    politi:inactetu ?val5 ;
    		politi:actempFem ?val6 ;
    		politi:iltEtr ?val7 ;
    		politi:popUrb ?val8 .
}
GROUP BY ?partiLabel
ORDER BY ?partiLabel

# Moyenne de la proportion de propriétaires dans
# les circonscriptions par parti politique

SELECT ?groupName (AVG(?val) AS ?avg)
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:memberOf ?group .
    
    ?group a politi:GroupeParlementaire ;
    	rdfs:label ?groupName.
    
    ?circo politi:proprio ?val .
}
GROUP BY ?groupName
ORDER BY DESC(?avg)

# Et par parti politique

SELECT ?groupName (AVG(?val) AS ?proprietaires)
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:PoliticalParty ?group .
    
    ?group rdfs:label ?groupName .
    
    ?circo politi:proprio ?val .
}
GROUP BY ?groupName
ORDER BY DESC(?proprietaires)

# Proportion de retraités par départements
# Renvoie également les coordonnées pour affichage sur une carte.

SELECT ?dep ?depLabel ?coord (AVG(?val) as ?avg)
WHERE {
    ?circ a politi:Circonscription ;
    rdfs:label ?circLabel ;
    schema:containedIn ?dep ;
    politi:inactret ?val .

    ?dep  rdfs:label ?depLabel ;
    politi:coord ?coord .
}GROUP BY ?dep ?depLabel ?coord



#-----------------------------------------------------------------------
#---------------------------- Ne marche pas ----------------------------
#-----------------------------------------------------------------------


# Députés nés dans une commune appartenant
# à leur circonscription actuelle.

#----- sans requête INSERT/DELETE --------
SELECT ?depuName ?CommuneNaissance
WHERE {
    ?depu a politi:Depute;
    	schema:name ?depuName;
    	schema:birthPlace ?CommuneNaissance;
    	politi:circonscription/schema:containsPlace/rdfs:label ?cirCommune.
    
        filter(?cirCommune = ?CommuneNaissance)
}

#----- après requête INSERT/DELETE --------
SELECT ?depuName ?comLabel
WHERE {
    ?depu a politi:Depute;
    	schema:name ?depuName;
    	schema:birthPlace ?CommuneNaissance;
    	politi:circonscription/schema:containsPlace ?CommuneNaissance.
    
        ?CommuneNaissance rdfs:label ?comLabel.
}


# Proportion des députés nés dans une commune
# appartenant à leur circonscription actuelle.

#----- sans requête INSERT/DELETE --------
SELECT (xsd:float(?depuNaissance)/xsd:float(COUNT(?depuAll)) AS ?proportion)
WHERE {
    ?depuAll a politi:Depute.
    
    {	
        SELECT (COUNT(?depu) AS ?depuNaissance)
        WHERE
        {
            ?depu a politi:Depute;
            schema:birthPlace ?CommuneNaissance;
    		politi:circonscription/schema:containsPlace/rdfs:label ?cirCommune.
    
        	filter(?cirCommune = ?CommuneNaissance)
    	}
    }
}GROUP BY ?depuNaissance

#----- après requête INSERT/DELETE --------
SELECT (xsd:float(?depuNaissance)/xsd:float(COUNT(?depuAll)) AS ?proportion)
WHERE {
    ?depuAll a politi:Depute.
    
    {	
        SELECT (COUNT(?depu) AS ?depuNaissance)
        WHERE
        {
            ?depu a politi:Depute;
            schema:birthPlace ?CommuneNaissance;
            politi:circonscription/schema:containsPlace ?CommuneNaissance.
    	}
    }
}GROUP BY ?depuNaissance

# Proportion des députés nés dans un département
# appartenant à leur circonscription actuelle.

#----- sans requête INSERT/DELETE --------
SELECT (xsd:float(?depuNaissance)/xsd:float(COUNT(?depuAll)) AS ?proportion)
WHERE {
    ?depuAll a politi:Depute.
    
    {	
        SELECT (COUNT(?depu) AS ?depuNaissance)
        WHERE
        {
            ?depu a politi:Depute;
            schema:birthPlace ?communeNaissance;
    		politi:circonscription/schema:containedIn ?dept.
    		
            ?commune rdfs:label ?comLabel; 
                politi:circonscription/schema:containedIn ?dept.
            
        	filter(?comLabel = ?communeNaissance)
    	}
    }
}GROUP BY ?depuNaissance

#----- après requête INSERT/DELETE --------

SELECT (xsd:float(?depuNaissance)/xsd:float(COUNT(?depuAll)) AS ?proportion)
WHERE {
    ?depuAll a politi:Depute.
    
    {	
        SELECT (COUNT(?depu) AS ?depuNaissance)
        WHERE
        {
            ?depu a politi:Depute;
            schema:birthPlace ?communeNaissance;
    		politi:circonscription/schema:containedIn ?dept.
    		
            ?communeNaissance politi:circonscription/schema:containedIn ?dept.
    	}
    }
}GROUP BY ?depuNaissance


# Moyenne de la proportion de propriétaires dans
# les circonscriptions par parti politique

SELECT ?partyAbbr ?partyName (AVG(?val) AS ?avg)
WHERE {
    ?depu a politi:Depute ;
    	politi:circonscription ?circo ;
    	schema:name ?depuLabel ;
    	schema:memberOf ?parti .
    
    ?parti a schema:PoliticalParty ;
    	rdfs:label ?partyName ;
    	schema:alternateName ?partyAbbr .
    
    ?circo politi:proprio ?val .
}
GROUP BY ?partyAbbr ?partyName
ORDER BY DESC(?avg)

# Requête finale
# On a inventé un index de fragilité du territoire
# On a regroupé les statistiques par département pour accentuer la dimension géographique et locale et éviter un biais trop local
# On a ensuite calculé les partis et groupes dominants dans ces départements

SELECT 
    ?depLabel
    (COUNT(DISTINCT ?depu) AS ?nbDeputes)
    (COUNT(DISTINCT ?parti) AS ?nbPartis)
    (AVG(?txPauv) AS ?avgPauvrete)
    (AVG(?age) AS ?ageMoyen)
    (AVG(?urb) AS ?populationUrb)
    (AVG(?inactRet) AS ?inactRetraites)
    (AVG(?inactEtu) AS ?inactEtudiants)
((AVG(?txPauv) * 0.6) + (AVG(?inactRet) * 0.2) + (AVG(?inactEtu) * 0.2) AS ?fragilityIndex)
    (SAMPLE(?coord) AS ?coordSample)
    (SAMPLE(?domPartyLabel) AS ?dominantParty)
    (SAMPLE(?domGroupLabel) AS ?dominantGroup)
WHERE {

    ?dep a politi:Departement ;
         rdfs:label ?depLabel .
    OPTIONAL { ?dep politi:coord ?coord . }

    ### CIRCO + SOCIO DATA
    ?circo schema:containedIn ?dep ;
           politi:txPauvrete60DiffTrageRF1 ?txPauv ;
           politi:ageMoyen ?age ;
           politi:popUrb ?urb ;
           politi:inactret ?inactRet ;
           politi:inactetu ?inactEtu .

    ### DEPUTE DATA
    ?depu a politi:Depute ;
          politi:circonscription ?circo ;
          schema:PoliticalParty ?parti ;
          schema:memberOf ?group .

    ?parti rdfs:label ?partiLabel .
    ?group rdfs:label ?groupLabel .

	### Parti maj par departement
    {
        SELECT ?dep (SAMPLE(?pLabel) AS ?domPartyLabel)
        WHERE {
            SELECT ?dep ?pLabel (COUNT(?depu) AS ?cnt)
            WHERE {
                ?depu a politi:Depute ;
                      politi:circonscription ?circoA ;
                      schema:PoliticalParty ?partiA .
                ?circoA schema:containedIn ?dep .
                ?partiA rdfs:label ?pLabel .
            }
            GROUP BY ?dep ?pLabel
        }
        GROUP BY ?dep
        ORDER BY DESC(?cnt)
    }

    ### Groupe maj par departement
    {
        SELECT ?dep (SAMPLE(?gLabel) AS ?domGroupLabel)
        WHERE {
            SELECT ?dep ?gLabel (COUNT(?depu) AS ?cnt2)
            WHERE {
                ?depu a politi:Depute ;
                      politi:circonscription ?circoB ;
                      schema:memberOf ?groupB .
                ?circoB schema:containedIn ?dep .
                ?groupB rdfs:label ?gLabel .
            }
            GROUP BY ?dep ?gLabel
        }
        GROUP BY ?dep
        ORDER BY DESC(?cnt2)
    }
}
GROUP BY ?depLabel
ORDER BY DESC(?fragilityIndex)

